Unicode true

!include "MUI2.nsh"
 
InstallDir "$PROGRAMFILES64\Galactic Solo"

# Retreive version and add it to the installer's name
Name "Galactic Solo"

# Set installer executable name
OutFile "output\galactic-solo-setup.exe"

# Unsure if required
RequestExecutionLevel admin

!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH

!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES

!insertmacro MUI_LANGUAGE "English"



Section

# Set output path
SetOutPath $INSTDIR

# File manually generated by instlog -- ensure it is always up-to-date!
# Include File calls to add all game files to the installer
!include "generated\install_files.nsi"
 
# Create uninstaller
WriteUninstaller $INSTDIR\uninstall.exe

CreateShortcut "$SMPROGRAMS\Galactic Solo.lnk" "$INSTDIR\Galactic Solo.exe"
CreateShortcut "$SMPROGRAMS\Uninstall Galactic Solo.lnk" "$INSTDIR\uninstall.exe"

# Add uninstaller to list of installed programs
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                 "DisplayName" "Galactic Solo"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                 "UninstallString" "$INSTDIR\uninstall.exe"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                 "Publisher" "Joshua Hartop"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                 "InstallLocation" "$INSTDIR"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                   "QuietUninstallString" "$\"$INSTDIR\uninstall.exe$\" /S"
WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                   "DisplayIcon" "$INSTDIR\Galactic Solo.exe"
WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                   "NoModify" 1
WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo" \
                   "NoRepair" 1

SectionEnd



Section "Uninstall"

# Remove the installation directory and shortcuts
# RMDir /r "$INSTDIR"

# File manually generated by instlog -- ensure it is always up-to-date!
# Includes Delete calls to remove all game files except the root $INSTDIR
!include "generated\uninstall_files.nsi"

Delete "$SMPROGRAMS\Galactic Solo.lnk"
Delete "$SMPROGRAMS\Uninstall Galactic Solo.lnk"
Delete "$INSTDIR\uninstall.exe"
RMDir "$INSTDIR"

# Remove from list of installed programs
DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\Galactic Solo"


SectionEnd